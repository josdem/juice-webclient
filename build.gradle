plugins {
	id 'org.springframework.boot' version '2.6.7'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "org.sonarqube" version "3.3"
	id 'jacoco'
	id 'java'
}

ext {
	artifactory_user = "$System.env.ARTIFACTORY_USER"
	artifactory_password = "$System.env.ARTIFACTORY_PASSWORD"
	hamcrestVersion = 2.2
}

apply plugin: 'maven-publish'

group = 'com.josdem.jugoterapia.webclient'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '16'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	testImplementation "org.hamcrest:hamcrest:$hamcrestVersion"
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
		html.destination file("${buildDir}/jacocoHtml")
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
		}
	}
	repositories {
		maven {
			url = "https://jfrog.josdem.io/artifactory/libs-snapshot-local"
			credentials {
				username = "$artifactory_user"
				password = "$artifactory_password"
			}
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
